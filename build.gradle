plugins {
    id "java"
    id "maven"
    id "org.jetbrains.kotlin.jvm" version "1.4.21"
    id "org.jetbrains.kotlin.kapt" version "1.4.21"
    id "org.jetbrains.dokka" version "1.4.20"
}

defaultTasks "clean", "build", "jar"

allprojects {

    def major = 1, minor = 0, patch = 0, type = "SNAPSHOT"

    group "eu.vironlab.vextension"
    version "$major.$minor.$patch-$type"

    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
        maven {
            name = 'papermc-repo'
            url = 'https://papermc.io/repo/repository/maven-public/'
        }
        maven {
            name = 'destroystokyo-repo'
            url = 'https://repo.destroystokyo.com/repository/maven-public/'
        }
        maven {
            name = 'velocitypowered-repo'
            url = 'https://repo.velocitypowered.com/releases/'
        }
        maven {
            name = 'minecraft-libraries'
            url = 'https://libraries.minecraft.net/'
        }
        maven {
            name = 'spongepowered-repo'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            name = "vironlab"
            url = "https://repo.vironlab.eu/repository/snapshot/"
        }
    }

    ext.gitCommitHash = { ->
        try {
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine "git", "rev-parse", "--short", "HEAD"
                standardOutput = stdout
            }
            return stdout.toString().trim()
        } catch (Exception ignored) {
            return "unknown"
        }
    }


    ext {
        project_version = "$major.$minor.$patch-$type"

        coords_mongo = "org.mongodb:mongodb-driver-sync:4.2.0"
        coords_gson = "com.google.code.gson:gson:2.8.6"
        coords_snakeyml = "org.yaml:snakeyaml:1.27"
        coords_jackson_xml = "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.12.1"
        coords_jackson = "com.fasterxml.jackson.core:jackson-databind:2.12.1"
        coords_jline = "jline:jline:2.14.6"

        //Minecraft APIs
        coords_bukkit = "com.destroystokyo.paper:paper-api:1.16.4-R0.1-SNAPSHOT"
        coords_sponge = "org.spongepowered:spongeapi:7.2.0"
        coords_waterfall = "io.github.waterfallmc:waterfall-api:1.16-R0.4-SNAPSHOT"
        coords_velocity = "com.velocitypowered:velocity-api:1.1.0"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.kotlin.kapt"
    apply plugin: "org.jetbrains.dokka"


    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    compileJava {
        options.encoding = "UTF-8"
    }

    compileTestJava {
        options.encoding = "UTF-8"
    }

    dependencies {
        compile("org.jetbrains.kotlin:kotlin-stdlib:1.4.21")
        compile("org.jetbrains.kotlin:kotlin-serialization:1.4.21")
        compile("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2")
    }

    jar.doFirst {
        manifest {
            attributes "Implementation-Title": project.name
            attributes "Implementation-Version": version
            attributes "Specification-Version": version
            attributes "Implementation-Vendor": "VironLab.eu"
            attributes "Built-By": System.getProperty("user.name")
            attributes "Build-Jdk": System.getProperty("java.version")
            attributes "Created-By": "Gradle " + gradle.gradleVersion
        }
    }

    jar {
        archiveFileName.set("${project.name}.jar")
    }

    sourcesJar {
        archiveFileName.set("${project.name}-sources.jar")
    }

    jar.doLast {
        pom {
            project {
                groupId = "eu.vironlab.vextension"
                artifactId = project.name
                version = project_version
                inceptionYear = "2021"
                licenses {
                    license {
                        name = "Apache License 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0"
                        distribution "repo"
                    }
                }
                developers {
                    developer {
                        id = "Infinity_dev"
                        name = "Florin Dornig"
                        email = "infinitydev@vironlab.eu"
                    }
                    developer {
                        id = "SteinGaming"
                        name = "Danial Daryab"
                        email = "steingaming@vironlab.eu"
                    }
                }
            }
        }.writeTo("build/pom/pom.xml")
    }


}
